// --[ Static analysis ]------------------------------------------------------------------------
apply plugin: 'checkstyle'      // Java source code style checking

// Checkstyle configuration.
checkstyle {
    configFile = rootProject.file("${project.projectDir}/gradle/checkstyle.xml")
    toolVersion = versions.checkstyle
    ignoreFailures = false
    checkstyleTest.enabled = false
}

checkstyleMain.source = "${project.projectDir}/src/main/java"
tasks.withType(Checkstyle) {
    exclude "**/db/migration/**"
    exclude "**/generated/**"

    reports {
        xml.enabled = true
        html.enabled = true
    }

    doLast {
        reports.all { report ->
            def outputFile = report.destination
            if (outputFile.exists() && outputFile.text.contains("<error")) {
//                        throw new GradleException("There were checkstyle warnings! For more info check file://${outputFile.absolutePath.replace(".xml", ".html")}")
                def reportPath = outputFile.absolutePath.replace(".xml", ".html")
                println("\n===========================================================" + "=" * reportPath.length())
                println("There were checkstyle warnings! For more info check file://${reportPath}")
                println("===========================================================" + "=" * reportPath.length())
            }
        }
    }
}
