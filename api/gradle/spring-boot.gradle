// --[ Additional config for Spring Boot projects ]---------------------------------------------
afterEvaluate {
    if (project.hasProperty("applicationType") && applicationType == "spring-boot") {

        println("[${project.name}] ====== Spring Boot project detected; applying extra configuration. ======")

        project.plugins.apply("org.springframework.boot")
        project.plugins.apply("io.spring.dependency-management")

        // Enable Spring dependency management.
        dependencyManagement {
            imports {
                mavenBom "org.springframework.cloud:spring-cloud-dependencies:${versions.springCloud}"
            }
        }

        // bootRun task for running Spring Boot apps.
        bootRun {
            // Enable coloured console output in IDE.
            jvmArgs = ["-Dspring.output.ansi.enabled=ALWAYS"]

            if (project.hasProperty("debugPort") && debugPort > 0) {
                println("[${project.name}] Remote debug port: ${debugPort}")
                jvmArgs += ["-Xdebug", "-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=${debugPort}"]
            }

            // Pass any JVM args through to the Spring app.
            systemProperties = System.properties
        }

        test {
            jvmArgs = ["-Dspring.profiles.active=test"]
        }
    }
}
